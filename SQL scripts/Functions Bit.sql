CREATE OR ALTER  FUNCTION [dbo].[CHECKBIT]
( 
	@EXPR AS VARBINARY(255), 
	@BIT AS INT 
)
	RETURNS INT
AS
BEGIN
	RETURN CASE 
			WHEN ( SUBSTRING( @EXPR, ( @BIT / 8 ) + 1, 1 ) 
							& POWER( 2, @BIT % 8 ) ) = POWER( 2, @BIT % 8 ) 
			THEN 1 ELSE 0 
			END
END
GO

CREATE OR ALTER FUNCTION [dbo].[SETBIT]
( 
	@EXPR AS VARBINARY(255), 
	@BIT AS INT , 
	@FLAG AS INT 
)
	RETURNS VARBINARY(255)
AS
BEGIN
	DECLARE @BYTE BINARY(1),
		    @MASK INT,
		    @INDEX INT
	
	SET @INDEX = @BIT / 8 + 1
	SET @MASK  = POWER( 2, @BIT % 8 )
	SET @BYTE =  CASE WHEN @FLAG = 1 
						THEN SUBSTRING( @EXPR, @INDEX , 1 ) | @MASK 
						ELSE SUBSTRING( @EXPR, @INDEX , 1 ) & ( ~@MASK ) 
				 END
	
	RETURN CONVERT( VARBINARY( 255 ) , STUFF(  @EXPR, @INDEX , 1 , @BYTE ) )
END